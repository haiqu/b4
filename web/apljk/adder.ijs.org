#+title: an adder in j:


* a half adder
:PROPERTIES:
:TS:       <2014-03-05 07:20AM>
:ID:       bdsk2970veg0
:END:

#+begin_src j
     ( ~:/~ ; *./~ ) 0 1   NB. sum is ~: (xor)   carry is *. (and)
  ┌───┬───┐
  │0 1│0 0│
  │1 0│0 1│
  └───┴───┘
#+end_src

#+begin_src j

    ha =: (~: , *.)"0

    (0 ha 0); (0 ha 1); (1 ha 0); (1 ha 1)
  ┌───┬───┬───┬───┐
  │0 0│1 0│1 0│0 1│
  └───┴───┴───┴───┘

    < @ ha /~ 0 1
  ┌───┬───┐
  │0 0│1 0│
  ├───┼───┤
  │1 0│0 1│
  └───┴───┘
#+end_src

#+begin_src j
     NB. compare to http://en.wikipedia.org/wiki/Adder_(electronics)
     A ,. B ,. C ,. S  NB. C=carry, S=sum for inputs A and B
  0 0 0 0
  1 0 0 1
  0 1 0 1
  1 1 1 0
#+end_src



* NOTE . representing a byte
:PROPERTIES:
:TS:       <2014-03-05 07:55AM>
:ID:       m80g9v80veg0
:END:

#+begin_src j
  
     byte =: (( _8 {. #: ) :. #. )"0
  
     byte 0 1 2 170 255
  0 0 0 0 0 0 0 0
  0 0 0 0 0 0 0 1
  0 0 0 0 0 0 1 0
  1 0 1 0 1 0 1 0
  1 1 1 1 1 1 1 1
  
     _ * byte 0 1 2 170 255
  0 0 0 0 0 0 0 0
  0 0 0 0 0 0 0 _
  0 0 0 0 0 0 _ 0
  _ 0 _ 0 _ 0 _ 0
  _ _ _ _ _ _ _ _
  
     (u: 9617 9619) {~ byte 0 1 2 170 255
  ░░░░░░░░
  ░░░░░░░▓
  ░░░░░░▓░
  ▓░▓░▓░▓░
  ▓▓▓▓▓▓▓▓
  
     (u: 9617 9608) {~ byte 0 1 2 170 255
  ░░░░░░░░
  ░░░░░░░█
  ░░░░░░█░
  █░█░█░█░
  ████████
  
     shade =: (u: 32 9617 9618 9619 9608) {~ ]
     widen =: (2: # ])"1
     drawbyte =: shade 1 + 3 * [: widen byte
     drawbyte 0 1 2 170 255
  ░░░░░░░░░░░░░░░░
  ░░░░░░░░░░░░░░██
  ░░░░░░░░░░░░██░░
  ██░░██░░██░░██░░
  ████████████████
 
#+end_src
  
